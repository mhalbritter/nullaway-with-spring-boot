plugins {
    id 'java'
    id 'org.springframework.boot' version '4.0.0-M2'
    id 'io.spring.dependency-management' version '1.1.7'
    id "net.ltgt.errorprone" version "4.3.0"
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'
description = 'Demo project for Spring Boot'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(24)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    errorprone "com.google.errorprone:error_prone_core:2.41.0"
    errorprone "com.uber.nullaway:nullaway:0.12.9"
}


tasks.withType(JavaCompile).configureEach {
    options.errorprone {
        disableAllChecks = true
        error("NullAway")
        option("NullAway:OnlyNullMarked", "true")
        option("NullAway:CustomContractAnnotations", "org.springframework.lang.Contract")
        option("NullAway:JSpecifyMode", "true")
    }
    if (name.toLowerCase().contains("test")) {
        options.errorprone {
            disable("NullAway")
        }
    }
}

tasks.named('test') {
    useJUnitPlatform()
}
